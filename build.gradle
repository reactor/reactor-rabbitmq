/*
 * Copyright (c) 2011-2022 VMware Inc. or its affiliates, All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/plugins-release" }
	}
}
plugins {
	alias(libs.plugins.asciidoctor.convert) apply false
	alias(libs.plugins.asciidoctor.pdf) apply false
	// applied in setup.gradle, only if artifactory credentials are configured
	alias(libs.plugins.artifactory) apply false
}

ext {
	//NOTE: all dependencies, including plugins, are defined in gradle/libs.versions.toml catalog

	jdk = JavaVersion.current().majorVersion
	jdkJavadoc = "https://docs.oracle.com/javase/$jdk/docs/api/"
	if (JavaVersion.current().isJava11Compatible()) {
		jdkJavadoc = "https://docs.oracle.com/en/java/javase/$jdk/docs/api/"
	}
	javadocLinks = [
		jdkJavadoc,
		"https://www.reactive-streams.org/reactive-streams-${libs.versions.reactiveStreams.get()}-javadoc/",
		"https://projectreactor.io/docs/core/release/api/",
		"https://rabbitmq.github.io/rabbitmq-java-client/api/current/"
	] as String[]
}

apply from: "${rootDir}/gradle/asciidoc.gradle"
apply from: "${rootDir}/gradle/releaser.gradle"

configurations.all {
	// check for snapshot updates every time
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

configure(allprojects) { project ->
	group = 'io.projectreactor.rabbitmq'

	repositories {
		mavenCentral()
		maven { url 'https://repo.spring.io/snapshot' }
	}

	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'java-library'
	apply from: "${rootDir}/gradle/setup.gradle"

	sourceCompatibility = targetCompatibility = 1.8

	if (JavaVersion.current().isJava8Compatible()) {
		compileTestJava.options.compilerArgs += "-parameters"
	}
	[compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:varargs",
															 "-Xlint:cast",
															 "-Xlint:classfile",
															 "-Xlint:dep-ann",
															 "-Xlint:divzero",
															 "-Xlint:empty",
															 "-Xlint:finally",
															 "-Xlint:overrides",
															 "-Xlint:path",
															 "-Xlint:processing",
															 "-Xlint:static",
															 "-Xlint:try",
															 "-Xlint:deprecation",
															 "-Xlint:unchecked",
															 "-Xlint:-serial",      // intentionally disabled
															 "-Xlint:-options",     // intentionally disabled
															 "-Xlint:-fallthrough", // intentionally disabled
															 "-Xlint:-rawtypes"     // TODO enable and fix warnings
	]

	dependencies {
		api libs.reactor.core
		api libs.rabbitmq.javaClient

		// JSR-305 annotations
		compileOnly libs.jsr305

		// Testing
		// JUnit Jupiter API and TestEngine implementation
		testImplementation platform(libs.junit.bom)
		testImplementation "org.junit.jupiter:junit-jupiter-api"
		testImplementation "org.junit.jupiter:junit-jupiter-params"
		testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"

		testImplementation libs.reactor.test
		testImplementation libs.mockito
		testImplementation libs.assertj
		testRuntimeOnly libs.slf4j.api
		testRuntimeOnly libs.slf4j.jcl
		testRuntimeOnly libs.logback
	}

	// force test runs even when there are no test changes
	test.outputs.upToDateWhen { false }
}

configure(rootProject) {
	archivesBaseName = 'reactor-rabbitmq'
	description = 'Reactor RabbitMQ: A reactive API for RabbitMQ'
	apply from: "${rootDir}/gradle/javadoc.gradle"

	jar {
		manifest {
			attributes("Automatic-Module-Name": "reactor.rabbitmq",
					   "Implementation-Version": project.version)
		}
	}

	test {
		useJUnitPlatform()
		systemProperty 'rabbitmqctl.bin', System.getProperty('rabbitmqctl.bin') ?: 'DOCKER:rabbitmq'
	}

	//add specific task artifacts to the publication
	publishing.publications.mavenJava.artifact(docsZip)
}

project(':reactor-rabbitmq-samples') {
	archivesBaseName = 'reactor-rabbitmq-samples'
	description = 'Samples for Reactor RabbitMQ'

	dependencies {
		implementation rootProject

		runtimeOnly libs.logback
		implementation libs.spring.boot.starter
		implementation libs.spring.amqp.rabbitmq

		testImplementation rootProject.sourceSets.test.output
		testRuntimeOnly libs.spring.boot.test
	}

	task sender(type:JavaExec) {
		mainClass.set('reactor.rabbitmq.samples.SampleSender')
		classpath = sourceSets.main.runtimeClasspath
	}

	task receiver(type:JavaExec) {
		mainClass.set('reactor.rabbitmq.samples.SampleReceiver')
		classpath = sourceSets.main.runtimeClasspath
	}

	task springboot(type:JavaExec) {
		mainClass.set('reactor.rabbitmq.samples.SpringBootSample')
		classpath = sourceSets.main.runtimeClasspath
	}

	test {
		useJUnitPlatform()
	}
}

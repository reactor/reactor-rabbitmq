/*
 * Copyright (c) 2011-2019 Pivotal Software Inc, All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
	repositories {
		maven { url "https://repo.spring.io/plugins-release" }
	}
	dependencies {
		classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.11',
				  'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
	}
}
plugins {
	id 'org.asciidoctor.convert' version '1.5.6'
	id 'me.champeau.gradle.jmh' version '0.4.7'
}

ext {
	gradleScriptDir = "${rootProject.projectDir}/gradle"

	reactorCoreVersion = "3.2.13.BUILD-SNAPSHOT"

	// Logging
	slf4jVersion = '1.7.26'
	logbackVersion = '1.2.3'

	// Libraries
	rabbitMqJavaClientVersion = '5.6.0'

	// Sample
	springBootVersion = '2.1.3.RELEASE'
	springAmqpVersion = '2.1.4.RELEASE'

	// Testing
	mockitoVersion = '2.25.1'
	junitPlatformVersion = '1.4.1'
	junitJupiterVersion  = '5.4.1'
	assertjVersion = '3.12.2'

	javadocLinks = ["https://docs.oracle.com/javase/7/docs/api/",
					"https://docs.oracle.com/javaee/6/api/",
					"https://www.reactive-streams.org/reactive-streams-1.0.2-javadoc/",
					"https://projectreactor.io/docs/core/release/api/",
					"https://rabbitmq.github.io/rabbitmq-java-client/api/current/",] as String[]
}

apply from: "$gradleScriptDir/setup.gradle"
apply from: "$gradleScriptDir/releaser.gradle"
apply from: "$gradleScriptDir/doc.gradle"

configurations.all {
	// check for snapshot updates every time
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

configure(allprojects) { project ->
	group = 'io.projectreactor.rabbitmq'

	repositories {
		maven { url 'https://repo.spring.io/libs-release' }
		maven { url 'https://repo.spring.io/libs-snapshot' }
		mavenCentral()
		jcenter()
	}

	apply plugin: 'maven'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'propdeps'
	apply plugin: 'java'

	sourceCompatibility = targetCompatibility = 1.8

	[compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:varargs",
															 "-Xlint:cast",
															 "-Xlint:classfile",
															 "-Xlint:dep-ann",
															 "-Xlint:divzero",
															 "-Xlint:empty",
															 "-Xlint:finally",
															 "-Xlint:overrides",
															 "-Xlint:path",
															 "-Xlint:processing",
															 "-Xlint:static",
															 "-Xlint:try",
															 "-Xlint:deprecation",
															 "-Xlint:unchecked",
															 "-Xlint:-serial",      // intentionally disabled
															 "-Xlint:-options",     // intentionally disabled
															 "-Xlint:-fallthrough", // intentionally disabled
															 "-Xlint:-rawtypes"     // TODO enable and fix warnings
	]

	dependencies {
		compile "io.projectreactor:reactor-core:$reactorCoreVersion"
		compile "com.rabbitmq:amqp-client:$rabbitMqJavaClientVersion"

		// Testing
		// JUnit Jupiter API and TestEngine implementation
		testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
		testCompile("org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}")
		testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")

		testCompile "io.projectreactor:reactor-test:$reactorCoreVersion"
		testCompile "org.mockito:mockito-core:$mockitoVersion"
		testCompile "org.assertj:assertj-core:${assertjVersion}"
		testRuntime "org.slf4j:jcl-over-slf4j:$slf4jVersion"
		testRuntime "org.slf4j:slf4j-api:$slf4jVersion"
		testRuntime "ch.qos.logback:logback-classic:$logbackVersion"
	}

	// force test runs even when there are no test changes
	test.outputs.upToDateWhen { false }
}

configure(rootProject) {
	archivesBaseName = 'reactor-rabbitmq'
	description = 'Reactor RabbitMQ: A reactive API for RabbitMQ'

	jar {
		manifest {
			attributes("Automatic-Module-Name": "reactor.rabbitmq",
					   "Implementation-Version": version)
		}
	}

	test {
		useJUnitPlatform()
		systemProperty 'rabbitmqctl.bin', System.getProperty('rabbitmqctl.bin') ?: 'sudo rabbitmqctl'
	}

	artifacts {
		archives sourcesJar
		archives javadocJar
		archives docsZip
	}

}

project(':reactor-rabbitmq-samples') {
	archivesBaseName = 'reactor-rabbitmq-samples'
	description = 'Samples for Reactor RabbitMQ'

	dependencies {
		compile rootProject
		runtime "ch.qos.logback:logback-classic:$logbackVersion"
		implementation "org.springframework.boot:spring-boot-starter:$springBootVersion"
		implementation "org.springframework.amqp:spring-rabbit:$springAmqpVersion"

		testCompile rootProject.sourceSets.test.output
		testRuntime "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
	}

	task sender(type:JavaExec) {
		main = 'reactor.rabbitmq.samples.SampleSender'
		classpath = sourceSets.main.runtimeClasspath
	}

	task receiver(type:JavaExec) {
		main = 'reactor.rabbitmq.samples.SampleReceiver'
		classpath = sourceSets.main.runtimeClasspath
	}

	task springboot(type:JavaExec) {
		main = 'reactor.rabbitmq.samples.SpringBootSample'
		classpath = sourceSets.main.runtimeClasspath
	}

	task classpath {
		doLast {
			println sourceSets.main.runtimeClasspath.asPath
		}
	}

	test {
		useJUnitPlatform()
	}

	artifacts {
		archives sourcesJar
		archives javadocJar
	}
}
